export DOTFILES_DIRS=$HOME/.dotfiles

# use .rcrc
if [[ -a ~/.rcrc ]]
then
  source ~/.rcrc
fi

# shortcut to this dotfiles path is $ZSH
export ZSH=$DOTFILES_DIRS/zsh

typeset -U config_files
config_files=($ZSH/**/*.zsh)
config_files=(${config_files:#*/*no-autoload*}) # remove the no autoload paths

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

OLDZSH=$ZSH # save path for our scripts
ZSH=$ZSH/oh-my-zsh.no-autoload # modify it for oh-my-zsh

# Oh My Zsh config
ZSH_THEME="aussiegeek"
plugins=(git bower brew bundler colorize django gem osx npm pip rbenv sublime sudo vundle)
source $ZSH/oh-my-zsh.sh
ZSH=$OLDZSH # just for further purpose
unset config_files