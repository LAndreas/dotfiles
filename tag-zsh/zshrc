export DOTFILES_DIRS=$HOME/.dotfiles

# use .rcrc
if [[ -a ~/.rcrc ]]
then
  source ~/.rcrc
fi

# shortcut to this dotfiles path is $ZSH
export ZSH=$DOTFILES_DIRS/zsh

# your project folder that we can `c [tab]` to
export PROJECTS=~/Workspace

# your ssh folder for pubkey command
export SSH_KEY_FOLDER=~/.ssh

# Path to your favorite SQLite Browser
export SQLITE_BROWSER_PATH=/Applications/SQLite\ Database\ Browser.app/Contents/MacOS/sqlitebrowser

typeset -U config_files
config_files=($ZSH/**/*.zsh)
config_files=(${config_files:#*/*no-autoload*}) # remove the no autoload paths

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

if [[ -d /usr/local/share/zsh/site-functions ]]; then
  # load every completion in defined folder
  for file in /usr/local/share/zsh/site-functions/*.zsh
  do
    source $file
  done
fi

if [[ -x `which brew` ]]
then
  source `brew --prefix`/etc/profile.d/z.sh
fi

if [[ -e ~/z/z.sh ]]
then
  source ~/z/z.sh
fi

unset config_files
